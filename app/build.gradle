apply plugin: 'com.android.application'
apply plugin: 'android-apt'
/**
 * Use the property (key/value pairs format) file secrets.properties
 * to configure api keys for your project.
 * Here is the list of necessary keys:
 * - DEBUG_NASA_NEOWS_API_KEY
 * - RELEASE_NASA_NEOWS_API_KEY
 */
def Properties projectProperties = new Properties()
projectProperties.load(new FileInputStream(new File(projectDir, '/secrets.properties')))

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "br.com.ecarrara.neom"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            buildConfigField "String", "NASA_NEOWS_API_KEY", "\"" + projectProperties.DEBUG_NASA_NEOWS_API_KEY + "\""
        }

        release {
            buildConfigField "String", "NASA_NEOWS_API_KEY", "\"" + projectProperties.RELEASE_NASA_NEOWS_API_KEY + "\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // app dependencies
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'


    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // unit test dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.3.0'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.mockito:mockito-core:2.1.0-beta.120'
}
